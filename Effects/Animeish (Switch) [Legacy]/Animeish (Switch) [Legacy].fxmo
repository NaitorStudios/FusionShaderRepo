/***********************************************************/

/* Shader author: Foxioo */
/* Version shader: 1.2 (18.10.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex
//version_####

uniform mat3 transformMatrix;
uniform mat3 projectionMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

in vec2 position;
out vec2 In;
void main()
{
    vec3 pos = vec3(position, 1.0);
    In = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End

/* ####################################################### */

//@Begin_fragment
//version_####

#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif

in vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform lowp vec4 blendColor;
uniform float fPixelWidth;
uniform float fPixelHeight;

/***********************************************************/
/* Varibles */
/***********************************************************/

uniform float _Mixing;
uniform float _ShadowThreshold;
uniform float _ShadowAlpha;
uniform float _OutlineThreshold;
uniform float _OutlineAlpha;
uniform float _OutlineScale;
uniform float _OutlineAntialiasing;
uniform float _ColorSteps;
uniform float _Saturation;
uniform vec4 _OutlineColor;
uniform bool _Blending_Mode;
uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

vec4 Fun_ColorRamp(vec4 _Color, float _Steps)
{
    _Color.rgb = floor(_Color.rgb * _Steps) / _Steps;
    return _Color;
}

out vec4 fragColor;
void main(void)
{
    vec2 In_Background = vec2(In.x, 1.0 - In.y);

    vec4 _Render_Texture = texture(imgTexture, In) * blendColor;
    vec4 _Render_Background = _Is_Pre_296_Build ? texture(bckgTexture, In_Background) : texture(bckgTexture, In);

	vec4 _Result, _GradientX, _GradientY;
    
            if(_Blending_Mode == false)
            {
                _Result = texture(imgTexture, In) * blendColor * _Mixing;
            }
            else
            {
                _Result = texture(bckgTexture, In) * _Mixing;
            }
    
        float _Average = (_Result.r + _Result.g + _Result.b) / 3.0;
    
    // Saturation
    _Result.rgb = _Average * (1.0 - (_Saturation / 50.0)) + _Result.rgb * (_Saturation / 50.0);
    
    // Shadow
    if(_Average < _ShadowThreshold)
        _Result.rgb *= _Average + (1.0 - (_ShadowAlpha + _ShadowThreshold));

        // Outline
        if(_Blending_Mode == false)
        {
            _GradientX = texture(imgTexture, In + vec2(fPixelWidth, 0.0)) * blendColor - texture(imgTexture, In - vec2(fPixelWidth, 0.0)) * blendColor;
            _GradientY = texture(imgTexture, In + vec2(0.0, fPixelHeight)) * blendColor - texture(imgTexture, In - vec2(0.0, fPixelHeight)) * blendColor;
        }
        else
        {
 			_GradientX = texture(bckgTexture, In + vec2(fPixelWidth, 0.0)) - texture(bckgTexture, In - vec2(fPixelWidth, 0.0));
        	_GradientY = texture(bckgTexture, In + vec2(0.0, fPixelHeight)) - texture(bckgTexture, In - vec2(0.0, fPixelHeight));       
        }
    
    float _EdgeDetection = (length(_GradientX.rgb) + length(_GradientY.rgb)) * _OutlineScale;
    float _AntialiasedOutline = smoothstep(_OutlineThreshold - _OutlineAntialiasing, _OutlineThreshold + _OutlineAntialiasing, _EdgeDetection);
    
    if (_AntialiasedOutline > _OutlineThreshold)
    {
        _Result = mix(_Result, _OutlineColor, _OutlineAlpha * _AntialiasedOutline);
    }
    
    // Color Ramp
    _Result = Fun_ColorRamp(_Result, _ColorSteps);
    
    _Result.a = _Render_Texture.a;

    fragColor = _Result;
}
//@End