// nbTexOverlay

//@Begin_vertex
//version_####

in vec2 position;

uniform mat3 projectionMatrix;
uniform mat3 transformMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

out vec2 textureCoordinate;

void main()
{
    vec3 pos = vec3(position, 1.0);
    textureCoordinate = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End
//@Begin_fragment
//version_####
#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif
in vec2 textureCoordinate;

uniform int tw;
uniform int th;
uniform float xOffset;
uniform float yOffset;
uniform float Intensity;

uniform sampler2D imgTexture;
uniform sampler2D Overlay;

uniform lowp vec4 blendColor;
uniform float fPixelWidth;
uniform float fPixelHeight;

out vec4 fragColor;

vec3 lerp(vec3 x, vec3 y, float s)
{
    return x + s*(y-x);
}

void main(void)
{
    //Get Texture scaling ratios.
    float ratioX=  (1.0/fPixelWidth) / float(tw);
    float ratioY=  (1.0/fPixelHeight) / float(th);

    vec2 uv = textureCoordinate;

    vec4 icolor = texture(imgTexture, uv);
    vec4 ocolor = vec4(0.0);
    if(icolor.a != 0.0)
    {
        ocolor = texture(Overlay, vec2((uv.x*ratioX + xOffset), (uv.y*ratioY + yOffset)));

        // Alpha Overlay
        float new_a = 1.0-(1.0-ocolor.a)*(1.0-icolor.a);
        ocolor.rgb = (ocolor.rgb*ocolor.a+icolor.rgb*icolor.a*(1.0-ocolor.a))/new_a;
        ocolor.rgb = mix(icolor.rgb, ocolor.rgb, Intensity);
        ocolor.a = icolor.a;
    }

    fragColor = ocolor * blendColor;
}
//@End