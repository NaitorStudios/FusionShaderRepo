/***********************************************************/

/* Autor shader: Foxioo */
/* Version shader: 1.1 (18.10.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex
//version_####

uniform mat3 transformMatrix;
uniform mat3 projectionMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

in vec2 position;
out vec2 In;
void main()
{
    vec3 pos = vec3(position, 1.0);
    In = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End

/* ####################################################### */

//@Begin_fragment
//version_####

#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif

#define RAD 0.0174532925

in vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform vec4 blendColor;

/***********************************************************/
/* Variables */
/***********************************************************/

uniform float _PosX;
uniform float _PosY;

uniform float _ScaleX;
uniform float _ScaleY;
uniform float _Scale;

uniform float _Mixing;
uniform bool _Blending_Mode;
uniform int _Looping_Mode;

uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

float Fun_Fade(float _Times)    { return _Times * _Times * _Times * (_Times * (_Times * 6.0 - 15.0) + 10.0); }
float Fun_Hash(vec2 _Pos)     {  return fract(sin(dot(_Pos, vec2(127.1, 311.7))) * 43758.5453); }
float Fun_PerlinNoise(vec2 _Pos)
{
    vec2 _I = floor(_Pos);
    vec2 _F = fract(_Pos);

    float _A = Fun_Hash(_I);
    float _B = Fun_Hash(_I + vec2(1.0, 0.0));
    float _C = Fun_Hash(_I + vec2(0.0, 1.0));
    float _D = Fun_Hash(_I + vec2(1.0, 1.0));

    vec2 _UV = vec2(Fun_Fade(_F.x), Fun_Fade(_F.y));

    return mix(mix(_A, _B, _UV.x), mix(_C, _D, _UV.x), _UV.y);
}

vec2 Fun_Offset(vec2 _Pos)
{
    vec2 _UV = _Pos * 0.5;

    float _OffsetX = Fun_PerlinNoise(_UV + sin(_PosX + _UV));
    float _OffsetY = Fun_PerlinNoise(_UV + cos(_PosX + _UV));

        _OffsetX = (_OffsetX * 2.0 - 1.0) * 0.1;
        _OffsetY = (_OffsetY * 2.0 - 1.0) * 0.1;

    return vec2(_OffsetX, _OffsetY);
}

out vec4 fragColor;
void main(void)
{
    vec2 In_Background = vec2(In.x, 1.0 - In.y);

    vec4 _Render_Texture = texture(imgTexture, In) * blendColor;

    vec2 UV;
    if(_Is_Pre_296_Build == true && _Blending_Mode == true)
    {
        UV = mix(In_Background, In_Background + (Fun_PerlinNoise(In_Background * vec2(_ScaleX, _ScaleY) * _Scale + vec2(_PosX, _PosY))), _Mixing);
    }
    else
    {
        UV = mix(In, In + Fun_PerlinNoise(In * vec2(_ScaleX, _ScaleY) * _Scale + vec2(_PosX, _PosY)), _Mixing);
    }

    vec4 _Render = vec4(0.0);

    if (_Looping_Mode == 0)
    {
        UV = fract(UV);
    }
    else if (_Looping_Mode == 1)
    {
        UV /= 2.0;
        UV = fract(UV);
        UV = abs(UV * 2.0 - 1.0);
    }
    else if(_Looping_Mode == 2)
    {
        UV = clamp(UV, 0.0, 1.0);
    }

    if(_Blending_Mode == false) {   _Render = texture(imgTexture, UV) * blendColor;  }
    else {                      _Render = vec4(texture(bckgTexture, UV).rgb, _Render_Texture.a);  }

        if (_Looping_Mode == 3 && (UV.x < 0.0 || UV.x > 1.0 || UV.y < 0.0 || UV.y > 1.0))
        {
            _Render = vec4(0.0);
        }

    fragColor = _Render;
}
//@End