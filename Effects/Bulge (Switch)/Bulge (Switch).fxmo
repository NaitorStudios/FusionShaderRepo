/***********************************************************/

/* Autor shader: Foxioo */
/* Version shader: 1.2 (18.10.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex
//version_####

uniform mat3 transformMatrix;
uniform mat3 projectionMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

in vec2 position;
out vec2 In;
void main()
{
    vec3 pos = vec3(position, 1.0);
    In = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End

/* ####################################################### */

//@Begin_fragment
//version_####

#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif

#define RAD 0.0174532925

in vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform vec4 blendColor;

/***********************************************************/
/* Variables */
/***********************************************************/

uniform float _PosX;
uniform float _PosY;

uniform float _RotX;

uniform float _PointX;
uniform float _PointY;

uniform float _ScaleX;
uniform float _ScaleY;
uniform float _Scale;

uniform float _Distortion;

uniform float _Mixing;
uniform bool _Blending_Mode;
uniform int _Looping_Mode;

uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

vec2 Fun_Bulge(vec2 In)
{
    vec2 _Center = vec2(_PointX, _PointY);
    vec2 _Rel = In - _Center;
    float _Ray = length(_Rel);

    float _Distance = _Ray * _Distortion;
    vec2 _UV = _Rel * _Distance;

    In = mix(_Rel, _UV, _Mixing) + _Center;

    return In;
}

vec2 Fun_RotationX(vec2 In)
{
    vec2 _Points = vec2(_PointX, _PointY);
    vec2 _UV = In;
    float _RotX_Fix = _RotX * (3.14159265 / 180.0);

    mat2 rotMat = mat2(
        cos(_RotX_Fix), sin(_RotX_Fix),
        -sin(_RotX_Fix), cos(_RotX_Fix)
    );

    _UV = _Points + rotMat * (_UV - _Points);

    return _UV;
}

out vec4 fragColor;
void main(void)
{
    vec2 In_Background = vec2(In.x, 1.0 - In.y);

        vec2 _Pos = vec2(_PosX, _PosY);
        vec2 _UV;

        if(_Is_Pre_296_Build && _Blending_Mode)
            _UV = Fun_RotationX(In_Background + _Pos);
        else
            _UV = Fun_RotationX(In + _Pos);

        _UV = ((_UV - vec2(_PointX, _PointY)) * vec2(_ScaleX, _ScaleY) * _Scale) + vec2(_PointX, _PointY);

    _UV = Fun_Bulge(_UV);

    if (_Looping_Mode == 0)
    {
        _UV = fract(_UV);
    }
    else if (_Looping_Mode == 1)
    {
        _UV /= 2.0;
        _UV = fract(_UV);
        _UV = abs(_UV * 2.0 - 1.0);
    }

    vec2 _UV_Background = vec2(_UV.x, 1.0 - _UV.y);

    vec4 _Render_Texture_UV = texture(imgTexture, _UV) * blendColor;
    vec4 _Render_Background_UV = texture(bckgTexture, _UV);

    vec4 _Result = _Blending_Mode ? _Render_Background_UV : _Render_Texture_UV;

        if (_Looping_Mode == 3 && (_UV.x < 0.0 || _UV.x > 1.0 || _UV.y < 0.0 || _UV.y > 1.0))
        {
            _Result = vec4(0.0);
        }

    _Result.a *= _Render_Texture_UV.a;

    fragColor = _Result;
}
//@End