/***********************************************************/

/* Shader author: Foxioo */
/* Version shader: 1.8 (18.10.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex
//version_####

uniform mat3 transformMatrix;
uniform mat3 projectionMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

in vec2 position;
out vec2 In;
void main()
{
    vec3 pos = vec3(position, 1.0);
    In = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End

/* ####################################################### */

//@Begin_fragment
//version_####

#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif

in vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform lowp vec4 blendColor;
uniform float fPixelWidth;
uniform float fPixelHeight;

/***********************************************************/
/* Varibles */
/***********************************************************/

uniform int _Blending_Mode;
uniform float _Mixing;
uniform float _Hue;
uniform float _Saturation;
uniform float _Intensity;
uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

float Fun_FMod(float a, float b) {
    return a - b * floor(a / b);
}

vec3 Fun_RGBtoHSI(vec3 _Render)
{
    float _Num = 0.5 * ((_Render.r - _Render.g) + (_Render.r - _Render.b));
    float _Den = sqrt((_Render.r - _Render.g) * (_Render.r - _Render.g) + (_Render.r - _Render.b)*(_Render.g - _Render.b));

    float _Theta = acos(clamp(_Num / _Den, 0.0, 1.0)) * 180.0 / 3.14159265;

        float _H        = (_Render.b <= _Render.g) ? _Theta : 360.0 - _Theta;

            float _Min    = min(_Render.r, min(_Render.g, _Render.b));
            float _Sum    = _Render.r + _Render.g + _Render.b;

        float _S = 1.0 - (3.0 * _Min / _Sum);
        float _I = _Sum / 3.0;

    return vec3(_H, _S, _I);
}

vec3 Fun_HSItoRGB(float _H, float _S, float _I)
{
    vec3 _Render;

    float _Rad = _H * 3.14159265 / 180.0;

    if (_H < 120.0)
    {
        _Render.b = _I * (1.0 - _S);
        _Render.r = _I * (1.0 + (_S * cos(_Rad)) / cos(3.14159265 / 3.0 - _Rad));
        _Render.g = 3.0 * _I - (_Render.r + _Render.b);
    }
    else if (_H < 240.0)
    {
        _H = _H - 120.0;

            _Rad = _H * 3.14159265 / 180.0;

        _Render.r = _I * (1.0 - _S);
        _Render.g = _I * (1.0 + (_S * cos(_Rad)) / cos(3.14159265 / 3.0 - _Rad));
        _Render.b = 3.0 * _I - (_Render.r + _Render.g);
    }
    else
    {
        _H = _H - 240.0;

            _Rad = _H * 3.14159265 / 180.0;

        _Render.g = _I * (1.0 - _S);
        _Render.b = _I * (1.0 + (_S * cos(_Rad)) / cos(3.14159265 / 3.0 - _Rad));
        _Render.r = 3.0 * _I - (_Render.g + _Render.b);
    }

    return (_Render);
}

out vec4 fragColor;
void main(void)
{
    vec2 In_Background = vec2(In.x, 1.0 - In.y);

    vec4 _Render_Texture = texture(imgTexture, In) * blendColor;
    vec4 _Render_Background = _Is_Pre_296_Build ? texture(bckgTexture, In_Background) : texture(bckgTexture, In) * blendColor;

    vec4 _Render =    _Blending_Mode == 1 ? _Render_Background : _Render_Texture;
    vec4 _Result =    _Render;

    /* Hue Adjustment */
        vec3 _HSI = Fun_RGBtoHSI(_Render.rgb);
            _HSI.x = Fun_FMod(_HSI.x + _Hue, 360.0);
            if (_HSI.x < 0.0) { _HSI.x += 360.0; }

    /* Saturation Adjustment */
        _HSI.y = (_HSI.y * (_Saturation / 50.0));

    /* Intensity Adjustment */
        _HSI.z = (_HSI.z + (_Intensity - 50.0) / 50.0);

    /* Back to RGB */
        _Render.rgb = Fun_HSItoRGB(_HSI.x, _HSI.y, _HSI.z);

    /* Mixing */
        _Render.rgb = mix(_Result.rgb, _Render.rgb, _Mixing);
    
    _Render.a = _Render_Texture.a;

    fragColor = _Render;
}
//@End