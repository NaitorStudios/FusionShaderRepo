/***********************************************************/

/* Shader author: Foxioo */
/* Version shader: 1.1 (18.10.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex
//version_####

uniform mat3 transformMatrix;
uniform mat3 projectionMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

in vec2 position;
out vec2 In;
void main()
{
    vec3 pos = vec3(position, 1.0);
    In = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End

/* ####################################################### */

//@Begin_fragment
//version_####

#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif

in vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform lowp vec4 blendColor;
uniform float fPixelWidth;
uniform float fPixelHeight;

/***********************************************************/
/* Varibles */
/***********************************************************/

uniform float _Mixing;
uniform float _Seed;
uniform float _PosX;
uniform float _PosY;
uniform float _Scale;
uniform float _ScaleX; 
uniform float _ScaleY;
uniform bool _Blending_Mode;
uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

float Fun_Hash21(vec2 _Pos) { return fract(sin(dot(_Pos, vec2(12.9898,78.233))) * 43758.5453); }
float Fun_Noise(vec2 _Pos)
{
    vec2 _I = floor(_Pos + _Seed);    vec2 _F = fract(_Pos);

        float _A = Fun_Hash21(_I + vec2(0.0, 0.0) + _Seed);
        float _B = Fun_Hash21(_I + vec2(1.0, 0.0) + _Seed);
        float _C = Fun_Hash21(_I + vec2(0.0, 1.0) + _Seed);
        float _D = Fun_Hash21(_I + vec2(1.0, 1.0) + _Seed);

    vec2 _UV = _F * _F * (3.0 - 2.0 *_F);

    return mix(mix(_A, _B, _UV.x), mix(_C, _D, _UV.x), _UV.y);
}

vec3 Fun_NoiseGradient(vec2 _UV)
{
    vec2 _Offset = vec2(12.9898, 78.233);
    float _Noise = Fun_Noise(_UV * 1.0 / vec2(fPixelWidth, fPixelHeight) * 0.25);

    float _Noise1 = Fun_Noise((_UV * 1.5 / vec2(fPixelWidth, fPixelHeight) * 0.25 + _Offset.yx) * 1.17);
    float _Noise2 = Fun_Noise((_UV * 1.5 / vec2(fPixelWidth, fPixelHeight) * 0.25 - _Offset.xy) * 1.23);
    
    vec3 _Render = mix(vec3(_Noise1), vec3(_Noise2), _Noise) * 1.35;

    return _Render;
}

vec3 Fun_NoiseSat(vec3 _Color, float _Sat)
{
    float _Lum = dot(_Color, vec3(0.299, 0.587, 0.114));

    return mix(vec3(_Lum, _Lum, _Lum), _Color, _Sat);
}

out vec4 fragColor;
void main(void)
{
    vec2 In_Background = vec2(In.x, 1.0 - In.y);

    vec4 _Render_Texture = texture(imgTexture, In) * blendColor;
    vec4 _Render_Background = _Is_Pre_296_Build ? texture(bckgTexture, In_Background) : texture(bckgTexture, In);

        vec2 _UV = (In + vec2(_PosX, _PosY)) * vec2(_ScaleX, _ScaleY) * _Scale; 
        vec4 _Result = vec4(0.0);

            _Result.rgb = Fun_NoiseGradient(_UV);

                vec4 _Render = _Blending_Mode ? mix(_Render_Background, vec4(_Result.rgb, 0), _Mixing) : mix(_Render_Texture, vec4(_Result.rgb, 0), _Mixing);
                _Render.a = _Render_Texture.a;

    fragColor = _Render;
}
//@End