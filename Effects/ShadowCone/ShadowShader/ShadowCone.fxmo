// ShadowCone

//@Begin_vertex
//version_####
#ifdef GL_ES
 precision mediump float;
#endif
in vec2 position;

uniform mat3 projectionMatrix;
uniform mat3 transformMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

out vec2 textureCoordinate;

void main()
{
    vec3 pos = vec3(position, 1.0);
    textureCoordinate = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End
//@Begin_fragment
//version_####
#ifdef GL_ES
precision mediump float;
#endif
in vec2 textureCoordinate;
out vec4 outColor;

uniform sampler2D imgTexture;

uniform int width;
uniform int height;
uniform int lightX;
uniform int lightY;
uniform int lightRadius;
uniform int lightAmbient;
uniform float coneAngle;
uniform float coneRange;

void main()
{
    float ambient = float(lightAmbient) / 255.0;
    vec4 outRGB;
    vec4 newRGB = texture(imgTexture, textureCoordinate);
    if (newRGB.a > 0.0)
    {
        outRGB = newRGB * ambient;
        outRGB.a = 1.0;
    }
    else
    {
        float rayDir;
        vec2 rayPos;
        vec2 rayMov;
        vec2 lightPos = vec2(float(lightX), float(lightY));
        vec2 pixelPos = vec2(textureCoordinate.x * float(width), textureCoordinate.y * float(height));
        float dist = distance(lightPos, pixelPos);
        outRGB = vec4(0.0, 0.0, 0.0, min(dist / float(lightRadius), 1.0 - ambient));

        if (dist <= float(lightRadius))
        {
            rayPos = lightPos;
            rayDir = atan(lightPos.y - pixelPos.y, lightPos.x - pixelPos.x);
            rayMov = vec2(cos(rayDir) * (dist / 88.0), sin(rayDir) * (dist / 88.0));
            float shortest_angle = mod(mod(rayDir - coneAngle, 6.28319) + 9.42478, 6.28319) - 3.14159;
            if (abs(shortest_angle) <= coneRange)
            {
                for (int i = 0; i < 88; i++)
                {
                    vec4 getRGB = texture(imgTexture, vec2(rayPos.x / float(width), rayPos.y / float(height)));
                    if (getRGB.a > 0.0)
                    {
                        outRGB = vec4(0.0, 0.0, 0.0, 1.0 - ambient);
                    }
                    rayPos -= rayMov;
                }
            }
            else
            {
                outRGB = vec4(0.0, 0.0, 0.0, 1.0 - ambient);
            }
        }
        else
        {
            outRGB = vec4(0.0, 0.0, 0.0, 1.0 - ambient);
        }
    }
    outColor = outRGB;
}
//@End