/***********************************************************/

/* Autor shader: Foxioo */
/* Version shader: 1.1 (18.10.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex
//version_####

uniform mat3 transformMatrix;
uniform mat3 projectionMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

in vec2 position;
out vec2 In;
void main()
{
    vec3 pos = vec3(position, 1.0);
    In = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End

/* ####################################################### */

//@Begin_fragment
//version_####

#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif

in vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform lowp vec4 blendColor;
uniform float fPixelWidth;
uniform float fPixelHeight;

/***********************************************************/
/* Varibles */
/***********************************************************/

uniform bool _Blending_Mode;
uniform float _Normal_Size;
uniform float _Mixing;
uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

float Fun_Luminance(vec3 _Result)
{
    const float _Kr = 0.299;
    const float _Kg = 0.587;
    const float _Kb = 0.114;

    float _Y = _Kr * _Result.r + _Kg * _Result.g + _Kb * _Result.b;

    return _Y;
}

out vec4 fragColor;
void main(void)
{
    vec2 In_Background = In;
    if(_Is_Pre_296_Build == true && _Blending_Mode == true) In_Background = vec2(In.x, 1.0 - In.y);

    vec4 _Render_Texture = texture(imgTexture, In) * blendColor;
    vec4 _Render_Background = texture(bckgTexture, In_Background);

    vec4 _Result = _Blending_Mode ? _Render_Background : _Render_Texture;

    vec2 _Off = _Normal_Size * vec2(fPixelWidth, fPixelHeight);
    float _Scale = _Mixing * max(1.0, _Normal_Size);
    
        vec2 _Emboss;

    if(_Blending_Mode == false)
    {
        vec3 _NW = texture(imgTexture, In + vec2(-_Off.x,  -_Off.y)).rgb * blendColor.rgb;
        vec3 _N  = texture(imgTexture, In + vec2(0.0,      -_Off.y)).rgb * blendColor.rgb;
        vec3 _NE = texture(imgTexture, In + vec2( _Off.x,  -_Off.y)).rgb * blendColor.rgb;
        vec3 _W  = texture(imgTexture, In + vec2(-_Off.x,   0.0))   .rgb * blendColor.rgb;
        vec3 _C  = texture(imgTexture, In)                          .rgb * blendColor.rgb;
        vec3 _E  = texture(imgTexture, In + vec2( _Off.x,   0.0))   .rgb * blendColor.rgb;
        vec3 _SW = texture(imgTexture, In + vec2(-_Off.x,  _Off.y)) .rgb * blendColor.rgb;
        vec3 _S  = texture(imgTexture, In + vec2(0.0,      _Off.y)) .rgb * blendColor.rgb;
        vec3 _SE = texture(imgTexture, In + vec2( _Off.x,  _Off.y)) .rgb * blendColor.rgb;

        _Emboss.x = ( Fun_Luminance(_NE) + 2.0 * Fun_Luminance(_E) + Fun_Luminance(_SE) ) - ( Fun_Luminance(_NW) + 2.0 * Fun_Luminance(_W) + Fun_Luminance(_SW) );
        _Emboss.y = ( Fun_Luminance(_SW) + 2.0 * Fun_Luminance(_S) + Fun_Luminance(_SE) ) - ( Fun_Luminance(_NW) + 2.0 * Fun_Luminance(_N) + Fun_Luminance(_NE) );
    }
    else
    {
        vec3 _NW = texture(bckgTexture, In + vec2(-_Off.x,  -_Off.y)).rgb;
        vec3 _N  = texture(bckgTexture, In + vec2(0.0,      -_Off.y)).rgb;
        vec3 _NE = texture(bckgTexture, In + vec2( _Off.x,  -_Off.y)).rgb;
        vec3 _W  = texture(bckgTexture, In + vec2(-_Off.x,   0.0))   .rgb;
        vec3 _C  = texture(bckgTexture, In)                          .rgb;
        vec3 _E  = texture(bckgTexture, In + vec2( _Off.x,   0.0))   .rgb;
        vec3 _SW = texture(bckgTexture, In + vec2(-_Off.x,  _Off.y)) .rgb;
        vec3 _S  = texture(bckgTexture, In + vec2(0.0,      _Off.y)) .rgb;
        vec3 _SE = texture(bckgTexture, In + vec2( _Off.x,  _Off.y)) .rgb;

        _Emboss.x = ( Fun_Luminance(_NE) + 2.0 * Fun_Luminance(_E) + Fun_Luminance(_SE) ) - ( Fun_Luminance(_NW) + 2.0 * Fun_Luminance(_W) + Fun_Luminance(_SW) );
        _Emboss.y = ( Fun_Luminance(_SW) + 2.0 * Fun_Luminance(_S) + Fun_Luminance(_SE) ) - ( Fun_Luminance(_NW) + 2.0 * Fun_Luminance(_N) + Fun_Luminance(_NE) );
    }

    vec3 _Normal = normalize(vec3(_Emboss.x, _Emboss.y, 1.0 / _Scale));
    _Normal = _Normal * 0.5 + 0.5;

        _Result.rgb = mix(_Result.rgb, _Normal, clamp(abs(_Mixing), 0.0, 1.0));
        _Result.a = _Render_Texture.a;

    fragColor = _Result;
}
//@End