// Quad

//@Begin_vertex
//version_####
in vec2 position;

uniform mat3 projectionMatrix;
uniform mat3 transformMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

out vec2 textureCoordinate;

void main()
{
    vec3 pos = vec3(position, 1.0);
    textureCoordinate = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End
//@Begin_fragment
//version_####
#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif
in vec2 textureCoordinate;

uniform float xA;
uniform float yA;
uniform float xB;
uniform float yB;
uniform float xC;
uniform float yC;
uniform float xD;
uniform float yD;

uniform sampler2D imgTexture;

uniform lowp vec4 blendColor;


out vec4 fragColor;

void main(void)
{
    float vD = xD + 0.0001;
    vec4 color = vec4( 0.0 );

    vec2 newInput = vec2(textureCoordinate.x, textureCoordinate.y);

    float a = (xA - newInput.x) * (yB - newInput.y) - (xB - newInput.x) * (yA - newInput.y);
    float b = (xB - newInput.x) * (yC - newInput.y) - (xC - newInput.x) * (yB - newInput.y);
    float c = (xC - newInput.x) * (yD - newInput.y) - (vD - newInput.x) * (yC - newInput.y);
    float d = (vD - newInput.x) * (yA - newInput.y) - (xA - newInput.x) * (yD - newInput.y);
    if (sign(a) == sign(b) && sign(b) == sign(c) && sign(c) == sign(d))
    {
        float a1 = xA;
        float a2 = xB - xA;
        float a3 = vD - xA;
        float a4 = xA - xB + xC - vD;


        float b1 = yA;
        float b2 = yB - yA;
        float b3 = yD - yA;
        float b4 = yA - yB + yC - yD;


        float aa = a4*b3 - a3*b4;
        float bb = a4*b1 -a1*b4 + a2*b3 - a3*b2 + newInput.x*b4 - newInput.y*a4;
        float cc = a2*b1 -a1*b2 + newInput.x*b2 - newInput.y*a2;
        float det = sqrt(bb*bb - 4.0*aa*cc);


        float m = (-bb+det)/(2.0*aa);
        float l = (newInput.x-a1-a3*m)/(a2+a4*m);


        color = texture(imgTexture, vec2( l, m ))*blendColor;
    }

    fragColor = color; //gl_FragColor
}
//@End
