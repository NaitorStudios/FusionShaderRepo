/***********************************************************/

/* Shader author: Foxioo */
/* Version shader: 1.1 (18.10.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex
//version_####

uniform mat3 transformMatrix;
uniform mat3 projectionMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

in vec2 position;
out vec2 In;
void main()
{
    vec3 pos = vec3(position, 1.0);
    In = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End

/* ####################################################### */

//@Begin_fragment
//version_####

#ifdef GL_ES
    #if defined(GL_FRAGMENT_PRECISION_HIGH) || defined(GL_OES_standard_derivatives)
        #define HIGH_PRECISION_SUPPORTED
    #endif
#else
    #define HIGH_PRECISION_SUPPORTED    
#endif

#ifdef HIGH_PRECISION_SUPPORTED
    precision highp float;
#else
    precision mediump float;
#endif

in vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform lowp vec4 blendColor;
uniform float fPixelWidth;
uniform float fPixelHeight;

/***********************************************************/
/* Varibles */
/***********************************************************/

uniform float _Mixing;
uniform vec4 _ColorShadow;
uniform vec4 _Color;
uniform vec4 _ColorLight;
uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

float Fun_Lum (vec4 _Result) { return (0.2126 * _Result.r + 0.7152 * _Result.g + 0.0722 * _Result.b); }

vec3 Fun_Outline(vec2 In, vec3 _Color)
{
    vec2 _PX = vec2(fPixelWidth, fPixelHeight);
    float _Alpha = texture(imgTexture, In).a;

    float aL1 = texture(imgTexture, In + vec2(-_PX.x, 0.0)).a;
    float aR1 = texture(imgTexture, In + vec2( _PX.x, 0.0)).a;
    float aU1 = texture(imgTexture, In + vec2(0.0, -_PX.y)).a;
    float aD1 = texture(imgTexture, In + vec2(0.0,  _PX.y)).a;

    float aL2 = texture(imgTexture, In + vec2(-_PX.x * 2.0, 0.0)).a;
    float aR2 = texture(imgTexture, In + vec2( _PX.x * 2.0, 0.0)).a;
    float aU2 = texture(imgTexture, In + vec2(0.0, -_PX.y * 2.0)).a;
    float aD2 = texture(imgTexture, In + vec2(0.0,  _PX.y * 2.0)).a;

        float _Edge1  = step(0.01, abs(aL1 - _Alpha) + abs(aR1 - _Alpha) + abs(aU1 - _Alpha) + abs(aD1 - _Alpha));
        float _Edge2 = step(0.01, abs(aL2 - _Alpha) + abs(aR2 - _Alpha) + abs(aU2 - _Alpha) + abs(aD2 - _Alpha));

    return mix(_Color, _Color + _Color * 0.25, _Edge1 * _Alpha) + _Edge2 * 0.15;
}

out vec4 fragColor;
void main(void)
{
    vec2 In_Background = vec2(In.x, 1.0 - In.y);

    vec4 _Render_Texture = texture(imgTexture, In) * blendColor;
    vec4 _Render_Background = _Is_Pre_296_Build ? texture(bckgTexture, In_Background) : texture(bckgTexture, In);

        float _Lum = pow(Fun_Lum(_Render_Texture), 2.0);
        float _Lum_Background = pow(Fun_Lum(_Render_Background), 2.0);

        vec4 _Result;

            _Result.rgb = mix(_ColorShadow.rgb, mix(_ColorLight.rgb, _Color.rgb, _Lum * _Lum), _Lum);

                vec2 _Center_Off = vec2(0.5, 1.0 - 0.2);
                vec2 _Center = vec2(0.5, 0.5);

                float _Dist = distance(In, _Center_Off);
                float _Dist_Cen = distance(In, _Center);

                        _Result.rgb += clamp(1.0 - (_Dist / 0.25), 0.0, 1.0) * 0.15;
                        _Result.rgb += clamp(1.0 - (_Dist / 0.75), 0.0, 1.0) * 0.35;

                    _Result.rgb += clamp((_Dist_Cen / 0.75), 0.0, 1.0) * 0.15;

                    /* Arc */
                        float _Inside = step(_Dist, 0.75);

                        float _Arc_Out = smoothstep(0.75 + 0.15, 0.75, _Dist);
                        _Result.rgb += _Arc_Out * (1.0 - _Inside) * 0.15;

                        float _Arc_In = smoothstep(0.75 - 0.4, 0.75, _Dist);
                        _Result.rgb += _Arc_In * (_Inside) * 0.15;

                    _Result.rgb = (Fun_Outline(In, _Result.rgb));

                float _Lum_Aero = Fun_Lum(vec4(_Result.rgb, 1.0));
                vec4 _Render_Background_Ex = _Is_Pre_296_Build ? texture(bckgTexture, In_Background - (_Lum_Aero * 0.25) + 0.125) : texture(bckgTexture, In - (_Lum_Aero * 0.25) + 0.125);                

            _Result.rgb += _Render_Background_Ex.rgb * _Lum * _Lum;
            _Result.rgb = mix(_Result.rgb, _Result.rgb * _Result.rgb * mix(vec3(1.0), _Result.rgb * _Lum, 0.5), vec3(0.85));

        _Result.a = _Render_Texture.a;
        _Result = mix(_Render_Texture, _Result * mix(1.0, _Lum, 0.1 * (1.0 - _Lum)), _Mixing);

    fragColor = _Result;
}
//@End